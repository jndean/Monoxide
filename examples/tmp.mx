
img_w = 28;
img_h = 28;


fn main()() {
    
    X = [];
    i = 0;
    while (i < img_w * img_h) {
        j = i;
        j => X;
        i += 1;
    } ~while (i > 0);

    X => reshape2D(img_w) => X;

    println('(', #X, ', ', #X[0], ')');

} ~main()


fn reshape2D(cols)(input) {

    output = [];
    tmp = [];
    
    while (#input) {
        x <= input;
        x => tmp;
    } ~while(#tmp);

    input ~= [];

    while (#tmp) {
        row = [];
        i = 0;
        while (i < cols) {
            x <= tmp;
            x => row;
            i += 1;
        } ~while(i > 0);
        i ~= cols;
        row => output;
    } ~while(#output);

} ~reshape2D(output)


fn matmul(X, Y)() 
{
    output = [];
    r = 0; while (r < #X) 
    {
        outrow = [];
        c = 0; while (c < #Y[0]) 
        {
            acc = 0;
            i = 0; while (i < #Y) 
            {
                acc += X[r][i] * Y[i][c];
                i += 1;
            } 
            ~while(i > 0); i ~= #Y;

            acc => outrow;
            c += 1;
        }
        ~while(c > 0); c ~= #Y[0];

        outrow => output;
        r += 1;
    }
    ~while(r > 0); r ~= #X;

} ~matmul(output)